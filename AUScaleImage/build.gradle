/*
 * Copyright 2013 Jacek Marchwicki <jacek.marchwicki@gmail.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    mavenCentral()
    maven {url 'https://commondatastorage.googleapis.com/maven-repository/'}
}

configurations {
    deployerJars
}

dependencies {
    deployerJars 'com.googlesource.gerrit:gs-maven-wagon:3.3.1'
    compile "com.appunite:android-support-v4:[7,)"
}


def baseVersionName = "1.4"
if (project.hasProperty("versionSuffix")) {
    project.ext.versionSuffix = project.versionSuffix
} else {
    project.ext.versionSuffix = "snapshot"
}
if (project.ext.versionSuffix) {
    version = "${baseVersionName}-${project.versionSuffix}"
} else {
    version = "${baseVersionName}"
}
archivesBaseName = "auscaleimage"
group = "com.appunite"

android {
    compileSdkVersion 17
    buildToolsVersion "17"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 17
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }
}

task createPomFile {
    project.ext.pomFile = pom {
        version = project.version
        artifactId = project.archivesBaseName
        groupId = project.group
        project {
            name 'AUScaleImage'
            description 'library for scrolling and scaling image.'
            url 'http://appunite.com'
            inceptionYear '2013'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                url "https://review.appunite.com/androidauscaleimage"
                connection "https://review.appunite.com/androidauscaleimage.git"
            }
            developers {
                developer {
                    name 'Jacek Marchwicki <jacek.marchwicki@gmail.com>'
                }
            }
        }
    }
}

task install(type: Upload, dependsOn: createPomFile) {
    description = "Upload archives to local repository"
    configuration = configurations.archives
    repositories.mavenInstaller {
        pom = project.ext.pomFile
    }
}

Properties props = new Properties()
def credentialsProperties = file("credentials.properties")
if (credentialsProperties.exists()) {
        props.load(new FileInputStream(credentialsProperties))
}

uploadArchives {
    description = "Upload archives to maven repository"
    dependsOn createPomFile
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "gs://maven-repository") {
                authentication(
                userName: "${props.getProperty('mavenUser', null)}" ,
                password: "${props.getProperty('mavenPassword', null)}")
            }
            pom = project.ext.pomFile
        }
    }
}

